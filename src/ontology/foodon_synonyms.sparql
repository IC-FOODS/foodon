PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo:  <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl:  <http://www.geneontology.org/formats/oboInOwl#>

SELECT DISTINCT ?class ?parent ?type ?label 
WHERE {

	# Enter ontology term identifier here to start report with:
	# obo:BFO_0000001 entity includes every term in ontology
	# obo:BFO_0000040 material entity, including chemical food components and food products
	# obo:ENVO_00010483  environmental material terms, including food materials
	# obo:FOODON_00002403 food material terms, including all food products and additives
	BIND (obo:BFO_0000040 as ?search). 	

	# Retrieve term and all subclass terms.
	?class rdfs:subClassOf* ?search.

	{
		# Retrieve one or more explicit parents of given entity
		{ 
			?class rdfs:subClassOf ?parent.
			# Ignore blank node parent (subclass axiom)
			FILTER (!isBlank(?parent)) 
		}

		# Retrieve entity's label(s). Might be multilingual.
		UNION { 
			?class rdfs:label ?label.
			BIND ('label' as ?type).
		}

		# Retrieve types of synonym
		UNION {
			?class oboInOwl:hasSynonym ?label.
			BIND ('synonym' as ?type).
		}
		UNION {
			?class oboInOwl:hasBroadSynonym ?label.
			BIND ('synonym (broad)' as ?type).
		}
		UNION {
			?class oboInOwl:hasExactSynonym ?label.
			BIND ('synonym (exact)' as ?type).
		}
		UNION {
			?class oboInOwl:hasNarrowSynonym ?label.
			BIND ('synonym (narrow)' as ?type).
		}
		UNION {
			?class obo:IAO_0000118 ?label.
			BIND ('label (alternative)' as ?type).
		}
	}
}
ORDER BY ?class ?type
